%{
open Peg_lib
open Markdown_lib
%}

Document        <- ((BlankLine* Block)* BlankLine*) EndOfFile


Block           <- BlockQuote / Verbatim / Reference / HTMLBlock 
                 / Heading / List / HorizontalRule / Para / Plain

BlockQuoteLine  <- NonIndentSpace '>' ' '? < anyLine >
                   { fun (_, _, v) -> v }

BlockQuote      <- (BlockQuoteLine+ (!BlankLine < anyLine >)* BlankLine*)+
                   { fun ls -> 
                       let txt = String.concat "" 
                         (List.concat (List.map (fun (l1, l2) -> l1 @ l2) ls)) in
                         BlackQuote [Markdown txt] 
                   }

Verbatim        <- (!BlankLine IndentedLine)+
                   ((Indent? BlankLine)+ (!BlankLine IndentedLine)+)* BlankLine*
                   { fun (i1, i2) -> 
                       Verbatim (String.concat "" 
                                ((List.concat [i1; List.concat i2]))) }

IndentedLine    <- Indent < AnyLine > { fun (_, _, v) -> v }
AnyLine         <- (!NewLine !EndOfFile .)* NewLine?

Reference       <- NonIndentSpace Label ':' SpNl < (!SpaceChar !NewLine .)+ >
                   SpNl Title? BlankLine*
                   { fun (l, ((_, _, s), t)) -> Reference (l, Src (s, t)) }

HorizontalRule  <- NonIndentSpace (('*' Sp '*' Sp '*' (Sp '*')*)
                                 / ('-' Sp '-' Sp '-' (Sp '-')*)
                                 / ('_' Sp '_' Sp '_' (Sp '_')*))
                   NewLine BlankLine+ # the + causes problems at the end of file
                   { fun _ -> HorizontalRule }

Para            <- Inline+ NewLine BlankLine+ { fun inlines -> Para inlines }
Plain           <- Inline+ BlankLine?         { fun inlines -> Plain inlines }

BlankLine       <- Sp NewLine

Inline          <- Strong / Emph / Code / EndLine / Spaces / Link / Image 
                 / AutoLink / RawHTML / Str / Entity / Special 

Spaces          <- spaceChar+ { fun () -> Space }

Strong           <- (StrongStar / StrongUl)     { fun inlines -> Strong inlines }

StrongStar       <- twoStar !' ' !NewLine (!(SpNl twoStar) Inline)+ twoStar
StrongUl         <- twoUl !' '  !NewLine (!(SpNl twoUl) Inline)+ twoUl !AlphaNum
  
Emph            <- (EmphStar / EmphUl)          { fun inlines -> Emph inlines }
EmphStar        <- oneStar !' ' !NewLine 
                          (Strong / !(SpNl oneStar) Inline)+ oneStar
EmphUl          <- oneUl !' ' !NewLine (Strong / !(SpNl oneUl) Inline)+ oneUl
                    !AlphaNum 

Code            <- Ticks@('`'+) < ((!'`' .)+ / !Ticks '`'+)+ > Ticks 
                   { fun (_, _, code) -> Code code }

Str             <- < NormalChar+ >  { fun (_, _, str) -> Text str }

Special         <- < SpecialChar >          { fun (_, _, v) -> Text v }

Link            <- ExplicitLink / ReferenceLink
Image           <- '!' Link  { fun (Link (x, y)) -> Image (x, y) }
AutoLink        <- AutoLinkURL / AutoLinkEmail

ReferenceLink   <- Label < SpNl > Label 
                   { fun (l1, ((_, _, s), l2)) -> Link (l2, Ref (l2, s)) }
                 / Label  { fun l -> Link (l, Nothing) }

Label           <- '[' (!']' Inline)* ']'
Title           <- ('"' < (!('\"' Sp (")" / NewLine)) !NewLine .)* > '"'
                 / '\'' < (!('\'' Sp (")" / NewLine)) !NewLine .)* > '\'')
                 { fun (_, _, v) -> v }

Source          <- ('<' < Source' > '>' / < Source' >) { fun (_, _, v) -> v }
Source'         <- ((![()> \n\t] .)+ / "(" Source' ")" / "<" Source' ">")*


SourceAndTitle  <- '(' Sp Source SpNl Title? Sp ')'

ExplicitLink    <- Label SpNl SourceAndTitle
                  { fun (l, (src, title)) -> Link (l, Src (src, title)) }

AutoLinkURL     <- '<' < Alpha+ "://" (!NewLine !'>' .)+ > '>' 
                   { fun (_, _, v) -> Link ([Text v], Src (v, None)) }

AutoLinkEmail   <- '<' < Alpha+ "@" (!NewLine !'>' .)+ > '>'
                { fun (_, _, v) -> Link ([Text v], Src (("mailto:" ^ v), None)) }

RawHTML         <- (< htmlComment / htmlTag >)  { fun (_, _, v) -> Html v }

htmlComment     <- "<!--" (!"-->" .)* "-->"
htmlTag         <- "<" SpNl "/"? AlphaNum+ SpNl htmlAttribute* "/"? ">"
htmlAttribute   <- AlphaNum+ SpNl ("=" SpNl (Quoted / (!Spaces .)+))? SpNl
Quoted          <- ["] (!'\"' .)* ["] / ['] (!'\'' .)* [']

htmlBlockAny    <- '<' SpNl '/' htmlBlockTag '>'

HTMLBlock       <- < NonIndentSpace (htmlComment / htmlBlockAny) >
                   { fun (_, _, v) -> HTMLBlock v }


Heading         <-  (atxHeading / setextHeading) BlankLine*

atxHeading     <- < "######" / "#####" / "####" / "###" / "##" / "#" >
                   (!EndLine !'#'  Inline)+ '#'* NewLine
                   { fun ((_, _, v), inlines) -> 
                       Heading (String.length v, inlines) }
setextHeading  <- (!EndLine Inline)+ NewLine ("==="  "="* { fun () -> 1 } 
                                             / "---" "-"* { fun () -> 2 }
                                             ) NewLine
                  { fun (inlines, level) -> Heading (level, inlines) }

List           <- BulletList / OrderedList

Bullet         <- NonIndentSpace < [+*\-] > SpaceChar+
BulletList     <- BulletListTight / BulletListLoose
BulletListTight <- (BulletListItem { fun s -> [Markdown s] })+
                   Blankline* doesNotMatch BulletListLoose 
                   { fun s -> BulletList s}
BulletListLoose <- (BulletListItem BlankLine*
                     { fun s -> [Markdown (s ^ "\n\n")] })+
                    { fun s -> BulletList s }
BulletListItem  <- !HorizontalRule Bullet < ListBlock ListContinuationBlock* >
ListBlock       <- < anyLine (!(Indent? (BulletListItem / OrderedListItem))
                   !BlankLine !(Indent (Bullet / Enumerator))
                   OptionallyIndentedLine)* >

ListContinuationBlock <- < BlankLine+ > / { fun () -> "\0" }
                         (Indent ListBlock)+

Enumerator      <- NonIndentSpace < Digit+ > '.' SpaceChar+
OrderedList     <- OrderedListTight / OrderedListLoose
OrderedListTight <- (OrderedListItem { fun s -> [Markdown s] })+ BlankLine*
                    !OrderedListLoose { fun s -> OrderedList s }
OrderedListLoose <- ((orderedListItem BlanklLne*) 
                    { fun s -> [Markdown (s ^ "\n\n")] } )+ 
                    { fun s -> OrderedList s }
OrderedListItem  <- Enumerator ListBlock < ListContinuationBlock* >

# Lexical syntax

EndOfFile       <- !.
NewLine         <- '\n' / "\r\n" / '\r'
Space           <- [ \t]
Sp              <- Space*
SpNl            <- Sp (NewLine Sp)?
Indent          <- '\t' / "    "
NonIndentSpace  <- ("   " / "  " / " ")?
twoStar         <- '**' !'**'
twoUl           <- '__' !'__'
oneStar         <- '*' !'*'
oneUl           <- '_' !'_'

Alpha           <- [a-zA-Z]
AlphaNum        <- [a-zA-Z0-9]
EscapedChar     <- '\\' .
SpecialChar     <- [*_`&\[\]<!\\]
NormalChar      <- EscapedChar / !(SpecialChar / ' ' / NewLine) .
EndLine         <- ' '? NewLine !BlankLine !EndOfFile { fun () -> Space }
Entity          <- '&'  < '#' ('x' / 'X') hexDigit+ 
                          / '#' Digit+ 
                          / AlphaNum+ > ';' { fun (_, _, v) -> Entity v }
Digit           <- [0-9]
hexDigit        <- [0-9a-fA-F]
spaceChar       <- ' '