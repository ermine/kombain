# It parsec YAML BNF

%{
open Kmb_grammar
open Kmb_input
open Yamlspec_types

let na = 0 (* shoild fail *)
%}

grammar         <- spacing definition+ spacing  endOfFile

definition      <- number nameFun defined_as expressionFun
                   { fun (m, (n, expr)) -> (m, RuleFun (n, expr)) }
                 / number name defined_as expression
                   { fun (m, (n, expr)) -> (m, Rule (n, expr)) }


expressionFun   <- cases / expression
cases           <- case+ { fun cs -> Cases cs }
case            <- expression l_ARROW expression

expression      <- name l_EQ element { fun (n, el) -> Cmp (n, el) }
                 / sequence (l_PIPE sequence)*   { make_alternates }

sequence         <- element*                   { make_sequence }

element         <- name " *< " var spacing
                   { fun (s, t) -> TimesLT (Identifier s, t) }
                 / name " *=< " var spacing
                   { fun (s, t) -> TimesLE (Identifier s, t) }
                 / name " * " times spacing 
                   { fun (s, t) -> t (Identifier s) }

                 / primary (" - " primary)+ spacing
                   { fun (p1, p2) -> Reject (p1, p2) }

                 / primary (l_STAR               { fun () v -> Star v }
                           / l_PLUS              { fun () v -> Plus v }
                           / l_QUESTION          { fun () v -> Opt v }
                           )? spacing
                   { fun (el, repeat) -> 
                       match repeat with None -> el | Some f -> f el }

primary         <- function { fun (n, ps) -> Function (n, ps) }
                 / name !l_EQ { fun n -> Identifier n }
                 / l_OPEN expression l_CLOSE
                 / l_literal
                 / l_class 

# Lex

space           <- " "
number          <- space* '[' < [0-9a-z]+ > ']' space*
identifier      <- < [a-zA-Z0-9] [a-zA-Z0-9\-+]* > 
name            <- identifier { fun v -> v.lexeme }
nameFun         <- name l_OPEN simpleParams l_CLOSE   
simpleParams    <- name (l_COMMA name)* { fun (s1, s2) -> s1 :: s2 }

function        <- name l_OPEN params? l_CLOSE
                   { fun (n, ps) -> match ps with None -> (n, [])
                                                | Some ps -> (n, ps) }
param           <- function  { fun (n, ps) -> Fn (n, ps) }
                 / name { fun n -> N n}
                 / < "-"> ? digits { fun (s, ds) -> match s with None -> Number ds 
| Some _ -> Number (- ds) }
params          <- param (l_COMMA param)* { fun (v, vs) -> v :: vs }

var             <- < [a-z] > { fun v -> v.lexeme }

l_COMMA           <- "," spacing
defined_as      <- space* "::=" spacing

l_literal         <- ('"""' { fun () -> [Char.code '\"'] }
                 / '"\"' { fun () -> [Char.code '\\'] }
                 / ["] (!["] l_char)* ["]
                 / ['] (!['] l_char)* [']
                 / l_hex { fun c -> [c]} )
                  { fun ls -> Literal ls }
l_class           <- "["  (!"]" range)* "]"     { fun cs -> Class cs }
range           <- l_hex '-' l_hex           { fun (c1, c2) -> Range (c1, c2) }
l_hex             <- '#x' < [0-9a-fA-F]+ >       { make_hex_char }
l_char            <- < . >                       { make_char }

l_QUESTION        <- '?' spacing
l_PLUS            <- '+' spacing
l_STAR            <- '*' spacing
l_PIPE            <- '|' spacing
l_OPEN            <- '(' spacing
l_CLOSE           <- ')'

newLine         <- [\r]? [\n]
comment         <- "/*" (!"*/" .)* "*/"
spacing         <- (comment / space / newLine)*
endOfFile       <- !.
digits           <- < [0-9]+ > { fun ds -> int_of_string ds.lexeme }
times           <- var { fun n -> fun s -> TimesVar (s, n) }
                / digits { fun ds -> fun s -> Times (s, ds) }
l_EQ            <- " = "
l_ARROW        <- "=> "
value          <- < [a-zA-z] [a-zA-Z\-]* > { fun v -> v.lexeme }
