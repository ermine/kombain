%{

open Kmb_input
open Kmb_grammar
open Printf

%}

# Hierarchical syntax

grammar		<- spacing declaration? definition+ endOfFile

declaration  <- '%{' < ( !'%}' .)* > '%}' spacing       { make_declaration }

definition   <- ident l_leftarrow expression { make_definition }
              / < . > { fun {start = (line, col)} ->
                          raise (Error (sprintf 
                            "Invalid rule definition at line %d col %d" 
                            line col)) 
                      }

expression	<- sequence (l_slash sequence)*       { make_alternates }

sequence	<- item* l_transform?                   { make_sequence }

item      <- ident l_at suffix               { make_pattern }
           / prefix

prefix		<- (l_and                             { make_predicate_and }
             / l_not                             { make_predicate_not }
             )? suffix                          { make_prefix }
                                                

suffix		<- primary ( l_question 		    { fun _ r -> Opt r }
			               / l_star 			      { fun _ r -> Star r }
			               / l_plus			        { fun _ r -> Plus r }
			               )?                 { fun (p, f) ->
                                            match f with
                                              | None -> p
                                              | Some f -> f p
                                        }

primary		<- ident !l_leftarrow       { make_name }
		       / l_open expression l_close
           / < l_open > { unmatched }
           / literal 
		       / l_class 
		       / l_dot
		       / l_leftangle expression l_rightangle   { make_tokenizer }
           / < l_leftangle >                       { unmatched }


# Lexical syntax

# Copied from OCaml's documentation
ident       <- < (letter / '_') (letter / [0-9_'])* > spacing
letter      <- [A-Za-z]

literal		  <- ['] (!['] char)* ['] spacing  { make_literal }
             / < ['] > { unmatched }
		         / ["] (!["] char)* ["] spacing  { make_literal }
             / < ["] > { unmatched }
l_class	    <- '[' (!']' range)* ']' spacing   { make_class }
             / < '[' > { unmatched }
range		    <- char '-' char                   { fun (c1, c2) -> Range (c1, c2) }
             / char < '-' >                    { fun (_, c) -> invalid_char c }
             / char                            { fun c -> Char c }
             / < . >                           { invalid_char }
char		    <- '\\' < [nrt'"\[\]\\] >          { make_escaped_char }
		         / '\\' < [0-3][0-7][0-7] >        { make_dec_char }
		         / '\\' < [0-7] [0-7]? >           { make_dec_char }
             / '\\x' < hex+ >                  { make_hex_char }
             / '\\' '-'                        { fun () -> Char.code '-' }
             / < '\\' >                        { invalid_char }
		         /  !'\\' < . >                    { make_char }
hex         <- [0-9a-fA-F]
l_leftarrow	<- '<-' spacing
l_slash	    <- '/' spacing
l_and	      <- '&' spacing
l_not	      <- '!' spacing
l_question	  <- '?' spacing
l_star		    <- '*' spacing
l_plus		    <- '+' spacing
l_open		    <- '(' spacing
l_close		  <- ')' spacing
l_dot		    <- '.' spacing                          { fun () -> Any }
spacing		  <- (space / comment)*
comment		  <- '#' (!endOfLine .)* endOfLine
space		    <- ' ' / '\t' / endOfLine
endOfLine	  <- '\r\n' / '\r' / '\n'
endOfFile	  <- !.

l_transform		  <- '{' < (l_transform' / !'}' .)* >  '}' spacing
               / < '{' > { unmatched }
l_transform'    <- '{' (l_transform' / !'}' .)* '}'
               / < '{' > { unmatched }

l_leftangle	  <- '<' spacing
l_rightangle	<- '>' spacing
l_at          <- '@' spacing